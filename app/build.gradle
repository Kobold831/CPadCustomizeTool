plugins {
    id 'com.android.application'
}

android {
    namespace = 'com.saradabar.cpadcustomizetool'
    compileSdk = 35

    defaultConfig {
        minSdk = 22
        //noinspection ExpiredTargetSdkVersion
        targetSdk = 22
        versionCode = 61
        versionName = "2.6.1"
        proguardFiles += "proguard-rules.pro"
        multiDexEnabled = true
    }

    signingConfigs {
        android {
            storeFile file('android.jks')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
    }

    buildTypes {
        configureEach {
            signingConfig = signingConfigs.android
        }
        release {
            minifyEnabled = true
            shrinkResources = true
        }
    }

    bundle {
        density {
            enableSplit = true
        }
    }

    buildFeatures {
        aidl = true
        buildConfig = true
    }

    lint {
        checkReleaseBuilds = false
        disable 'ProtectedPermissions', 'ExportedService', 'UnusedAttribute'
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

dependencies {
    // Custom Libraries
    compileOnly files('libs/BenesseExtensionLibrary.jar')
    implementation fileTree(dir: 'libs', include: ['IDcha*.jar'])

    // AndroidX
    implementation 'androidx.preference:preference:1.2.1'

    // Google Material You
    implementation 'com.google.android.material:material:1.12.0'

    // Welcome
    // https://jitpack.io/com/github/stephentuso/welcome-android/v1.4.1/welcome-android-v1.4.1-release.aar
    implementation files('libs/welcome-android.aar')
    // https://jitpack.io/#stephentuso/welcome-android/v1.4.1
    // タグ(v1.4.1)とアーティファクト名(v1.4.1-release)が異なるため使用不可
    //implementation 'com.github.stephentuso:welcome-android:v1.4.1'

    // ZIP
    implementation 'org.zeroturnaround:zt-zip:1.17'

    // Dhizuku
    implementation 'io.github.iamr0s:Dhizuku-API:2.4'

    // HiddenApiBypass
    compileOnly 'dev.rikka.hidden:stub:4.3.3'
}
